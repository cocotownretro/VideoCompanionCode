5 'SG24 BASE CODE
9 'RESERVE 6K FOR SG24 SCREEN, ARRAYS FOR COLORS (FULL,LEFT,RIGHT PIXEL) AND VDG DIGITS
10 PCLEAR4:DIM CF(8),CL(8),CR(8),DG(9)
11 DIM F9(2) : ' GET/PUT BUFFER FOR FALLING 9
14 'SET UP COLORS & DIGITS ARRAYS. EFFECTIVE RESOLUTION IS 32 X 192 WITH 9 COLORS
15 GOSUB 31010
16 ' DISPLAY MENU OF CHOICES
17 GOSUB 20000
19 'SET UP SCREEN SIZE TO MATCH SG24
20 PMODE 4,1
24 'CLEAR SCREEN TO BLACK (CF(0)). NOTE: PCLS DOES NOT USE POKE 178 VALUE, SO WE CLEAR WITH LINE ,PSET,BF.I DID 1/4 AND USED PCOPY FOR REST (FASTER)
25 POKE178,CF(0):LINE(0,0)-(255,47),PSET,BF:SCREEN1,0:FORC=2TO4:PCOPY1TOC:NEXT
29 'DISABLE PRINT HOOK SO BASIC DOESN'T FORCE BACK TO SG4 OR REGULAR PMODE 4. ALSO DO GIME POKE AS WELL (NO EFFECT ON COCO 1/2).  T1 TWEAK: "AND &HF" MASKS OUT T1 BITS
30 POKE 359,57:POKE 65314,PEEK(65314)AND &HF:POKE&HFF9C,&HA
34 ' RUN SELECTED DEMO
35 ON DM GOTO 40,100,300,600,900,1100
39 ' HORIZONTAL STRIPES
40 FOR Y=0 TO 191 STEP 18
45 FOR C=0 TO 8
50 POKE178,CF(C):LINE(0,Y+C*2)-(255,Y+C*2),PSET
60 NEXT C,Y
70 GOTO 30000
99 ' VERTICAL STRIPES (L,R)
100 FOR X = 0 TO 255 STEP 72
110 FOR C=0 TO 8
120 POKE178,CL(C):LINE(X+C*8,0)-(X+C*8+7,191),PSET,BF
130 NEXT C,X
200 FOR X = 0 TO 255 STEP 72
210 FOR C=0 TO 8
220 POKE178,CR(C):LINE(X+C*8,0)-(X+C*8+7,191),PSET,BF
230 NEXT C,X
240 GOTO 30000
299 ' VERTICAL STRIPES NASTY (F,L,R)
300 FOR X = 0 TO 255 STEP 9
310 FOR C=0 TO 8
320 POKE178,CF(C):LINE(X+C,0)-(X+C,191),PSET
330 NEXT C,X
400 FOR X = 0 TO 255 STEP 9
410 FOR C=0 TO 8
420 POKE178,CL(C):LINE(X+C,0)-(X+C,191),PSET
430 NEXT C,X
500 FOR X = 0 TO 255 STEP 9
510 FOR C=0 TO 8
520 POKE178,CR(C):LINE(X+C,0)-(X+C,191),PSET
530 NEXT C,X
540 GOTO 30000
599 ' DIAGONALS
600 FOR C=1 TO 8
605 S$="COLOR:" + STR$(C):X=96:Y=0:GOSUB 21000
610 POKE178,CF(C):GOSUB 700
640 NEXT C
650 GOTO 30000
699 ' SUBROUTINE: DIAGONAL LINES IN CURRENT COLOR
700 FOR Y=0 TO 191 STEP 3
710 LINE(0,Y)-(128,96),PSET:LINE-(255,Y),PSET
720 NEXT Y
730 FOR X=0 TO 255 STEP 3
740 LINE(X,12)-(128,96),PSET:LINE-(X,191),PSET
750 NEXT X
760 RETURN
800 X=0:Y=0
805 FOR CH=&80 TO &H17F
810 VG = ((&HFF-CH)*3-&H3D) AND &HFF
820 POKE 178,VG
830 LINE(X,Y)-(X+7,Y+11),PSET,BF
840 X = X + 8
850 IF X > 255 THEN X = 0:Y = Y + 12
860 NEXT CH
899 ' FALLING 9
900 S$="REDRAW EACH TIME":X=0:Y=0:GOSUB 21000
910 FOR Y = 1 TO 191-11
920 POKE 178,CF(0):LINE (136,Y-1)-(143,Y-1),PSET: 'ERASE TOP LINE FROM PREVIOUS POSITION
930 POKE 178,DG(9):LINE (136,Y)-(143,Y+11),PSET,BF: ' DRAW DIGIT
940 NEXT Y
950 POKE 178,CF(0):LINE (136,191-11)-(143,191),PSET,BF: 'ERASE
960 S$="   USE GET / PUT":X=0:Y=0:GOSUB 21000
970 POKE 178,DG(9):LINE (136,1)-(143,12),PSET,BF: ' DRAW DIGIT
980 GET(136,1)-(143,12),F9: 'GET IT TO BUFFER
990 POKE 178,CF(0) : ' BLACK FOR ERASING
1010 FOR Y = 1 TO 191-11
1020 LINE (136,Y-1)-(143,Y-1),PSET: 'ERASE TOP LINE FROM PREVIOUS POSITION
1030 PUT (136,Y)-(143,Y+11),F9,PSET: ' PASTE DIGIT
1040 NEXT Y
1050 GOTO 30000
1099 ' ALPHANUMERIC PRINT TEST
1100 S$="@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]"+CHR$(94)+CHR$(95):X=0:Y=0:GOSUB 21000
1110 S$=" !"+CHR$(&H22)+"#$%&'()*+,-./0123456789:;<=>?":X=0:Y=12:GOSUB 21000
1120 S$=" abcdefghijklmnopqrstuvwxyz":X=0:Y=24:GOSUB 21000
1125 ' TRY OUT THE T1 FLAGS: LOWERCASE, REVERSE, BACKGROUND-COLOR BORDER
1126 ' (SOME OF THESE WILL LOOK ODD ON NON-T1)
1130 GOSUB 1300
1140 PK = PEEK(65314)
1150 POKE 65314,(PK OR &H10):GOSUB 1300
1160 POKE 65314,(PK OR &H20):GOSUB 1300
1170 POKE 65314,(PK OR &H40):GOSUB 1300
1180 POKE 65314,PK
1199 GOTO 30000
1299 ' PAUSE
1300 FOR I=1 TO 1500:NEXT I:RETURN
20000 'STARTUP MENU
20010 CLS:PRINT"1) HORIZONTAL STRIPES"
20020 PRINT"2) VERTICAL STRIPES (L,R)"
20030 PRINT"3) VERTICAL STRIPES (F,L,R) >:)"
20040 PRINT"4) DIAGONALS >:)"
20050 PRINT"5) FALLING 9 >:)"
20060 PRINT"6) ALPHANUMERIC PRINT TEST"
20910 A$=INKEY$
20920 IF A$ < "1" OR A$ > "6" THEN 20910
20930 DM = VAL(A$)
20940 RETURN
20999 ' SUBROUTINE TO DRAW ANY ALPHANUMERIC STRING
21000 FOR I=1 TO LEN(S$)
21010 CH=ASC(MID$(S$,I,1)): ' GRAB ASCII VALUE OF CHARACTER
21011 ' CONVERT ASCII TO VDG BYTE (TRANSLATED FROM ROM'S PUTCHR)
21012 IF CH < &H40 THEN CH = (CH OR &H40) - (CH AND &H40):GOTO 21020
21014 IF CH < &H60 THEN 21020
21016 CH = CH AND &HDF:CH = (CH OR &H40) - (CH AND &H40)
21020 CH = ((&HFF-CH)*3+3) AND &HFF: 'CONVERT VDG BYTE TO SEMIGRAPHICS "COLOR" VALUE
21030 POKE 178,CH
21040 LINE(X,Y)-(X+7,Y+11),PSET,BF
21050 X = X + 8
21060 NEXT I
21070 RETURN
29999 'RE-ENABLE PRINT HOOK AGAIN FOR NORMAL OPERATION
30000 IF INKEY$="" THEN 30000
30010 POKE 359,126:GOTO 17: ' RETURN TO TEXT MODE, DISPLAY STARTUP MENU AGAIN
31000 'INITIALIZE CF/CL/CR (FULL WIDTH/LEFT PIXEL/RIGHT PIXEL COLORS AND DG (DIGITS) (THE LATTER FOR VDG TEXT)
31010 C$="205323F3C39363330300"
31020 D$="706D6A6764615E5B585552"
31030 FORA=0TO8:CF(A)=VAL("&H"+MID$(C$,A*2+1,2)):DG(A)=VAL("&H"+MID$(D$,A*2+1,2)):NEXT:DG(9)=VAL("&H"+MID$(D$,19,2))
31040 C$="20623202D2A2724212"
31050 D$="20714111E1B1815121"
31060 FORA=0TO8:CL(A)=VAL("&H"+MID$(C$,A*2+1,2)):CR(A)=VAL("&H"+MID$(D$,A*2+1,2)):NEXT:RETURN
